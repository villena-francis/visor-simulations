
configfile: "config/config.yaml"


GENOME_FILE = config['genome_file']

STAGE_LIST = config['stage_list']

COVERAGE = config['coverage']

LENGTH = config['length']

REPLICATES_LIST = config['replicates']

CALLERS_LIST = config['callers']


def get_resource(rule, resource):
    try:
        return config["resources"][rule][resource]
    except KeyError:
        return config["resources"]["default"][resource]


rule all:
    input:
        [f"results/{stage}_{coverage}x_{length}_{replicate}_{caller}" \
        for stage in STAGE_LIST \
        for coverage in COVERAGE \
        for replicate in REPLICATES_LIST \
        for length in LENGTH \
        for caller in CALLERS_LIST]


rule visor_hack:
    input:
        fasta = f"resources/genome/{GENOME_FILE}",
        bed1 = f"resources/{{stage}}/HACk.h1.bed",
        bed2 = f"resources/{{stage}}/HACk.h2.bed"
    output:
        h1_fa = f"results/{{stage}}_HACk/h1.fa",
        h1_fa_fai = f"results/{{stage}}_HACk/h1.fa.fai",
        h2_fa = f"results/{{stage}}_HACk/h2.fa",
        h2_fa_fai = f"results/{{stage}}_HACk/h2.fa.fai",
        folder = directory(f"results/{{stage}}_HACk")
    log:
        f"results/log/visor_hack/{{stage}}.log"
    benchmark:
        f"results/log/visor_hack/{{stage}}.bmk"
    threads: get_resource("visor_hack", "threads")
    resources:
        mem_mb=get_resource("visor_hack", "mem_mb"),
        runtime=get_resource("visor_hack", "runtime"),
        slurm_partition=get_resource("visor_hack", "partition")
    conda:
        "envs/visor.yaml"
    shell:"""
        VISOR HACk \
            -b {input.bed1} {input.bed2} \
            -g {input.fasta} \
            -o {output.folder} \
            > {log} 2>&1
    """


rule generate_bed:
    input:
        rules.visor_hack.output.h1_fa,
        rules.visor_hack.output.h2_fa,
        rules.visor_hack.output.h1_fa_fai,
        rules.visor_hack.output.h2_fa_fai,
    output:
        f"results/{{stage}}_HACk/laser.simple.bed"
    log:
        f"results/log/generate_bed/{{stage}}.log"
    benchmark:
        f"results/log/generate_bed/{{stage}}.bmk"
    threads: get_resource("generate_bed", "threads")
    resources:
        mem_mb=get_resource("generate_bed", "mem_mb"),
        runtime=get_resource("generate_bed", "runtime"),
        slurm_partition=get_resource("generate_bed", "partition")
    shell:"""
        sh workflow/scripts/makeBED.sh {wildcards.stage} {GENOME_FILE} \
        > {log} 2>&1
    """   


rule visor_laser_tumour:
    input:
        fasta = f"resources/genome/{GENOME_FILE}",
        hack_output = f"results/{{stage}}_HACk",
        bed = f"results/{{stage}}_HACk/laser.simple.bed"
    output:
        bam = f"results/{{stage}}_{{coverage}}x_{{length}}_{{replicate}}_LASeR/sim.srt.bam",
        folder = directory(f"results/{{stage}}_{{coverage}}x_{{length}}_{{replicate}}_LASeR")
    log:
        f"results/log/visor_laser/{{stage}}_{{coverage}}x_{{length}}{{replicate}}.log"
    benchmark:
        f"results/log/visor_laser/{{stage}}_{{coverage}}x_{{length}}_{{replicate}}.bmk"
    threads: get_resource("visor_laser", "threads")
    resources:
        mem_mb=get_resource("visor_laser", "mem_mb"),
        runtime=get_resource("visor_laser", "runtime"),
        slurm_partition=get_resource("visor_laser", "partition"),
        slurm_extra=get_resource("visor_laser", "slurm_extra") 
    conda:
        "envs/visor.yaml"
    params:
        read_type=config['params']['read_type']
    shell:"""
        VISOR LASeR \
            -g {input.fasta} \
            -s {input.hack_output} \
            -b {input.bed} \
            -o {output.folder} \
            --threads {threads} \
            --coverage {wildcards.coverage} \
            --length_mean {wildcards.length} \
            --fastq --compress \
            --read_type {params.read_type} \
            > {log} 2>&1
    """


rule visor_laser_normal:
    input:
        fasta = f"resources/genome/{GENOME_FILE}",
        hack_output = f"results/normal_HACk",
        bed = f"results/normal_HACk/laser.simple.bed"
    output:
        bam = f"results/normal_{{coverage}}x_{{length}}_LASeR/sim.srt.bam",
        folder = directory(f"results/normal_{{coverage}}x_{{length}}_LASeR")
    log:
        f"results/log/visor_laser/normal_{{coverage}}x_{{length}}.log"
    benchmark:
        f"results/log/visor_laser/normal_{{coverage}}x_{{length}}.bmk"
    threads: get_resource("visor_laser", "threads")
    resources:
        mem_mb=get_resource("visor_laser", "mem_mb"),
        runtime=get_resource("visor_laser", "runtime"),
        slurm_partition=get_resource("visor_laser", "partition"),
        slurm_extra=get_resource("visor_laser", "slurm_extra") 
    conda:
        "envs/visor.yaml"
    params:
        read_type=config['params']['read_type']
    shell:"""
        VISOR LASeR \
            -g {input.fasta} \
            -s {input.hack_output} \
            -b {input.bed} \
            -o {output.folder} \
            --threads {threads} \
            --coverage {wildcards.coverage} \
            --length_mean {wildcards.length} \
            --fastq --compress \
            --read_type {params.read_type} \
            > {log} 2>&1
    """


rule sniffles:
    input:
        bam = f"results/{{stage}}_{{coverage}}x_{{length}}_{{replicate}}_LASeR/sim.srt.bam",
        fasta = f"resources/genome/{GENOME_FILE}"
    output:
        directory(f"results/{{stage}}_{{coverage}}x_{{length}}_{{replicate}}_sniffles"),
        vcf = f"results/{{stage}}_{{coverage}}x_{{length}}_{{replicate}}_sniffles/{{stage}}.vcf"
    log:
        f"results/log/sniffles/{{stage}}_{{coverage}}x_{{length}}_{{replicate}}.log"
    benchmark:
        f"results/log/sniffles/{{stage}}_{{coverage}}x_{{length}}_{{replicate}}.bmk"
    threads: get_resource("sniffles", "threads")
    resources:
        mem_mb=get_resource("sniffles", "mem_mb"),
        runtime=get_resource("sniffles", "runtime"),
        slurm_partition=get_resource("sniffles", "partition")
    conda:
        "envs/sniffles.yaml"
    params:
        vntrs=config['params']['vntrs']
    shell:"""
        sniffles \
            -i {input.bam} \
            -v {output.vcf} \
            -t {threads} \
            --reference {input.fasta} \
            --tandem-repeats {params.vntrs} \
            --mosaic \
            > {log} 2>&1
    """


rule severus:
    input:
        tumour_bam = f"results/{{stage}}_{{coverage}}x_{{length}}_{{replicate}}_LASeR/sim.srt.bam",
        normal_bam = f"results/normal_{{coverage}}x_{{length}}_LASeR/sim.srt.bam"
    output:
        dir = directory(f"results/{{stage}}_{{coverage}}x_{{length}}_{{replicate}}_severus")
    log:
        f"results/log/severus/{{stage}}_{{coverage}}x_{{length}}_{{replicate}}.log"
    benchmark:
        f"results/log/severus/{{stage}}_{{coverage}}x_{{length}}_{{replicate}}.bmk"
    threads: get_resource("severus", "threads")
    resources:
        mem_mb=get_resource("severus", "mem_mb"),
        runtime=get_resource("severus", "runtime"),
        slurm_partition=get_resource("severus", "partition")
    conda:
        "envs/severus.yaml"
    params:
        vntrs=config['params']['vntrs']
    shell:"""
        ln -sf $(basename {input.tumour_bam}) $(dirname {input.tumour_bam})/tumour.bam
        ln -sf $(basename {input.tumour_bam}).bai $(dirname {input.tumour_bam})/tumour.bam.bai
        severus \
            --target-bam $(dirname {input.tumour_bam})/tumour.bam \
            --control-bam {input.normal_bam} \
            --out-dir {output.dir} \
            --vntr-bed {params.vntrs} \
            -t {threads} \
            > {log} 2>&1
    """


rule svision:
    input:
        tumour_bam = f"results/{{stage}}_{{coverage}}x_{{length}}_{{replicate}}_LASeR/sim.srt.bam",
        normal_bam = f"results/normal_{{coverage}}x_{{length}}_LASeR/sim.srt.bam",
        fasta = f"resources/genome/{GENOME_FILE}"
    output:
        dir = directory(f"results/{{stage}}_{{coverage}}x_{{length}}_{{replicate}}_svision"),
    log:
        f"results/log/svision/{{stage}}_{{coverage}}x_{{length}}_{{replicate}}.log"
    benchmark:
        f"results/log/svision/{{stage}}_{{coverage}}x_{{length}}_{{replicate}}.bmk"
    threads: get_resource("svision", "threads")
    resources:
        mem_mb=get_resource("svision", "mem_mb"),
        runtime=get_resource("svision", "runtime"),
        slurm_partition=get_resource("svision", "partition"),
        slurm_extra=get_resource("svision", "slurm_extra"),
    conda:
        "envs/svision-pro.yaml"
    params:
        model=get_resource("svision", "model"),
        stage= f"{{stage}}",
        preset="error-prone"
    shell:"""
        ln -sf $(basename {input.tumour_bam}) $(dirname {input.tumour_bam})/tumour.bam
        ln -sf $(basename {input.tumour_bam}).bai $(dirname {input.tumour_bam})/tumour.bam.bai
        SVision-pro \
            --target_path $(dirname {input.tumour_bam})/tumour.bam \
            --base_path {input.normal_bam} \
            --genome_path {input.fasta} \
            --model_path {params.model} \
            --out_path {output.dir} \
            --sample_name {params.stage} \
            --preset {params.preset} \
            --detect_mode somatic \
            --process_num {threads} \
            > {log} 2>&1
    """


rule savana:
    input:
        tumour_bam = f"results/{{stage}}_{{coverage}}x_{{length}}_{{replicate}}_LASeR/sim.srt.bam",
        normal_bam = f"results/normal_{{coverage}}x_{{length}}_LASeR/sim.srt.bam",
        fasta = f"resources/genome/{GENOME_FILE}"
    output:
        dir = directory(f"results/{{stage}}_{{coverage}}x_{{length}}_{{replicate}}_savana")
    log:
        f"results/log/savana/{{stage}}_{{coverage}}x_{{length}}_{{replicate}}.log"
    benchmark:
        f"results/log/savana/{{stage}}_{{coverage}}x_{{length}}_{{replicate}}.bmk"
    threads: get_resource("savana", "threads")
    resources:
        mem_mb=get_resource("savana", "mem_mb"),
        runtime=get_resource("savana", "runtime"),
        slurm_partition=get_resource("savana", "partition")
    conda:
        "envs/savana.yaml"
    shell:"""
        ln -sf $(basename {input.tumour_bam}) $(dirname {input.tumour_bam})/tumour.bam
        ln -sf $(basename {input.tumour_bam}).bai $(dirname {input.tumour_bam})/tumour.bam.bai
        savana run \
            --tumour $(dirname {input.tumour_bam})/tumour.bam \
            --normal {input.normal_bam} \
            --outdir {output.dir} \
            --ref {input.fasta} \
            --threads {threads}
            > {log} 2>&1
        savana classify \
            --vcf {output.dir}/tumour.sv_breakpoints.vcf \
            --ont \
            --output {output.dir}/tumour.classified.vcf \
            --somatic_output {output.dir}/tumour.classified.somatic.vcf \
            >> {log} 2>&1
    """